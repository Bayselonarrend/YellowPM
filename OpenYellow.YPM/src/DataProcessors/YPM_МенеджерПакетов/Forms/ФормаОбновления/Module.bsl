#Область ОбработчикиСобытийФормы
    
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    //@skip-check wrong-string-literal-content
    ОписаниеОбновления = Параметры["Описание"];
    //@skip-check wrong-string-literal-content
    URLЗагрузки        = Параметры["URLФайла"];
    //@skip-check wrong-string-literal-content
    НоваяВерсия        = Параметры["НоваяВерсия"];
    
    //@skip-check wrong-string-literal-content
    Элементы.ТекущаяВерсия.Заголовок = Параметры["ТекущаяВерсия"];
    Элементы.НоваяВерсия.Заголовок   = НоваяВерсия;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОтветНет(Команда)
    Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ОтветДа(Команда)
    
    ООп    = Новый ОписаниеОповещения("ПослеВыбораПути", ЭтотОбъект, URLЗагрузки);
    Режим  = РежимДиалогаВыбораФайла.Сохранение;                   
    Диалог = Новый ДиалогВыбораФайла(Режим);
    Диалог.ПолноеИмяФайла = "YPM_" + НоваяВерсия + ".cfe";
    Диалог.МножественныйВыбор = Ложь;
    Диалог.Показать(ООп);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеВыбораПути(Результат, АдресФайла) Экспорт
    
    #Если Не ВебКлиент Тогда
    	
    Сообщение = "";
    
	Попытка
		Если Не Результат = Неопределено Тогда
			
			YPM_ИнструментыКлиентСервер.Get(АдресФайла, Результат[0]);
			Сообщение = "Файл успешно загружен. Теперь необходимо обновить расширение в конфигураторе!";
			
		КонецЕсли;
		
	Исключение
		Сообщение = "Ошибка при загрузке файла: " + ОписаниеОшибки();	
	КонецПопытки;
    
    #КонецЕсли
    
    #Если ВебКлиент Тогда
    	Сообщение = "Получение файлов недоступно в веб-клиенте!";  
    #КонецЕсли
    
    Закрыть(Сообщение);
     
КонецПроцедуры

#КонецОбласти
